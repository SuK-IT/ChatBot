//file:noinspection GroovyAssignabilityCheck
import java.text.SimpleDateFormat
import org.siouan.frontendgradleplugin.infrastructure.gradle.RunNpmYarn

plugins {
    id 'java'
    id 'idea'

    id "org.siouan.frontend-jdk8" version "5.3.0"

    id 'org.springframework.boot' version '2.5.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

group = 'de.sukit'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url 'https://repo.spring.io/release' }
}

sourceSets {
    main {
        java {
            srcDirs = ['backend/main/java']
        }
        resources {
            srcDirs = ['backend/main/resources']
        }
    }
    test {
        java {
            srcDirs = ['backend/test/java']
        }
        resources {
            srcDirs = ['backend/test/resources']
        }
    }
}

configurations {
    all {
        // We only want slf4j and log4j.
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }

    embed
    implementation.extendsFrom(embed)
}

dependencies {
    embed 'commons-io:commons-io:2.11.0'
    embed 'org.springframework.boot:spring-boot-starter-validation:2.5.4'
    embed 'org.springframework.boot:spring-boot-starter-web:2.5.4'
    embed 'org.springframework.boot:spring-boot-starter-log4j2:2.5.4'
    embed 'org.apache.logging.log4j:log4j-web:2.14.1'

    developmentOnly 'org.springframework.boot:spring-boot-devtools:2.5.4'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.5.4'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'

}

test {
    useJUnitPlatform()

    // Mention test result in logs
    testLogging {
        events "passed",
                "skipped",
                "failed"
    }
}

frontend {
    packageJsonDirectory = file("${projectDir}/frontend")

    nodeVersion = '16.1.0'
    nodeInstallDirectory = file("${projectDir}/frontend/node")

    yarnEnabled = false
    yarnVersion = "1.22.10"
    yarnInstallDirectory = file("${projectDir}/frontend/yarn")

    cleanScript = 'run clean'
    assembleScript = 'run build'

    // Print the architecture we are running on.
    System.out.println(String.format("I am running on: %s", System.getProperty("os.arch")))

    // If we are running on arm, specify Node path pattern so arm-builds succeed.
    if (System.getProperty("os.arch").equals("arm")) {
        nodeDistributionUrlPathPattern = 'vVERSION/node-vVERSION-linux-armv7l.TYPE'
    } else if (System.getProperty("os.arch").equals("aarch64")) {
        nodeDistributionUrlPathPattern = 'vVERSION/node-vVERSION-linux-arm64.TYPE'
    }
}

// Configure frontend plugin. See documentation at https://siouan.github.io/frontend-gradle-plugin/.
frontend {
    packageJsonDirectory = file("${projectDir}/frontend")

    nodeVersion = '16.9.1'
    nodeInstallDirectory = file("${projectDir}/frontend/node")

    yarnEnabled = false
    yarnVersion = "1.22.11"
    yarnInstallDirectory = file("${projectDir}/frontend/yarn")

    cleanScript = 'run clean'
    assembleScript = 'run build'

    // Print the architecture we are running on.
    System.out.println(String.format("I am running on: %s", System.getProperty("os.arch")))

    // If we are running on arm, specify Node path pattern so arm-builds succeed.
    if (System.getProperty("os.arch").equals("arm")) {
        nodeDistributionUrlPathPattern = 'vVERSION/node-vVERSION-linux-armv7l.TYPE'
    } else if (System.getProperty("os.arch").equals("aarch64")) {
        nodeDistributionUrlPathPattern = 'vVERSION/node-vVERSION-linux-arm64.TYPE'
    }
}

// Include specific files in resources folder, like the license and readme.
tasks.register('about', Copy) {
    dependsOn tasks.named('clean')

    from layout.projectDirectory.file("LICENSE") into layout.projectDirectory.dir("backend/main/resources")
    from layout.projectDirectory.file("README.md") into layout.projectDirectory.dir("backend/main/resources")
}


tasks.register('installQuasar', RunNpmYarn) {
    dependsOn tasks.named('installNode')
    script = 'install -g @quasar/cli'
}

// Custom task to build and copy an up-to-date version of our frontend to SpringBoot.
tasks.register('copyDist', Copy) {
    dependsOn tasks.named('assembleFrontend')
    // Delete old frontend files from SpringBoot.
    def dirName = "backend/main/resources/static"
    file( dirName ).list().each{
        f ->
            delete "${dirName}/${f}"
    }

    // Copy new frontend files to SpringBoot.
    from layout.projectDirectory.dir("frontend/dist/spa")
    into layout.projectDirectory.dir("backend/main/resources/static")
}

// Make sure everything is included in our JavaDoc.
// Since this project is open source, we can include private etc. classes and methods docs.
tasks.withType(Javadoc) {
    options.addStringOption('encoding', 'UTF-8')
}
javadoc {
    options.memberLevel = JavadocMemberLevel.PRIVATE
    classpath = sourceSets.main.runtimeClasspath
}

// Build JavaDoc and JavaSources JARs.
java {
    withSourcesJar()
    withJavadocJar()
}

bootJar {
    // Customize MANIFEST to include relevant information.
    manifest {
        attributes  "Start-Class"    : "de.sukit.chatbot.Main",
                "Description"    : "Chatbot in Java as backend and Quasar as frontend.",
                "Built-By"       : System.getProperty("user.name"),
                "Build-Timestamp": new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                "Created-By"     : "Gradle ${gradle.gradleVersion}",
                "Build-Jdk"      : "${System.getProperty('java.version')} (${System.getProperty('java.vendor')} ${System.getProperty('java.vm.version')})",
                "Build-OS"       : "${System.getProperty('os.name')} ${System.getProperty('os.arch')} ${System.getProperty('os.version')}"
    }
}

// JAR configuration still needed for Launch4j.
jar {
    // No duplicates
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    // Include all libraries in our JAR-file.
    from {
        configurations.embed.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }

    // Customize MANIFEST to include relevant information.
    manifest {
        attributes(
                "Main-Class"     : "de.sukit.chatbot.Main",
                "Class-Path"     : configurations.embed.findAll { it.name.endsWith('jar') }.collect { zipTree(it) },
                "Description"    : "Chatbot in Java as backend and Quasar as frontend.",
                "Built-By"       : System.getProperty("user.name"),
                "Build-Timestamp": new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                "Created-By"     : "Gradle ${gradle.gradleVersion}",
                "Build-Jdk"      : "${System.getProperty('java.version')} (${System.getProperty('java.vendor')} ${System.getProperty('java.vm.version')})",
                "Build-OS"       : "${System.getProperty('os.name')} ${System.getProperty('os.arch')} ${System.getProperty('os.version')}",
        )
    }

}
